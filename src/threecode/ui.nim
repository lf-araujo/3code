## REPL command dispatch and provider/model wizards.
##
## Handles all `:cmd` input that is not a prompt to the model. The command set
## is narrow by design: introspection (`:show`, `:log`, `:tokens`), context
## management (`:clear`, `:summarize`), provider/model switching
## (`:provider`, `:model`, `:reasoning`).
##
## Tab-completion in `tabComplete` walks `KnownGoodCombos` and the live
## provider list to offer only valid model names. The provider-add wizard in
## `runProviderAdd` validates the API key with a one-token probe before saving.

import std/[algorithm, atomics, json, os, sequtils, strformat, strutils, tables, terminal, times]
import types, util, prompts, session, config, api, compact, display, minline,
  fatprompt, streamexec

const CommandNames* = [":help", ":tokens", ":clear", ":model", ":provider",
                      ":reasoning", ":streaming", ":notify", ":prompt", ":show",
                      ":log", ":sessions", ":summarize", ":version",
                      ":q", ":quit", ":exit"]

type WizardReadLineHook* = proc(prompt: string, hidden,
                                noHistory: bool): string {.closure.}

type
  CommandKind* = enum
    ckUnknown, ckSafeImmediate, ckMutating, ckModal, ckQuit
  CommandDisposition* = enum
    cdTranscriptResult, cdHarnessOnly, cdModal
  CommandResult* = object
    recognized*: bool
    ok*: bool
    name*: string
    body*: string
    plainBody*: bool
    clearFooter*: bool
    disposition*: CommandDisposition

var wizardReadLineHook*: WizardReadLineHook

proc handleCommandResult*(cmd: string, messages: var JsonNode,
                          session: var Session, prof: var Profile,
                          editor: var minline.LineEditor): CommandResult

proc classifyCommand*(cmd: string): CommandKind =
  let c = cmd.strip
  if c.len == 0 or c[0] != ':':
    return ckUnknown
  let sp = c.find({' ', '\t'})
  let name = if sp < 0: c else: c[0 ..< sp]
  let arg = if sp < 0: "" else: c[sp+1 .. ^1].strip
  let parts = arg.splitWhitespace()
  case name
  of ":help", ":?", ":tokens", ":show", ":log", ":sessions", ":prompt", ":version":
    ckSafeImmediate
  of ":streaming", ":notify":
    if parts.len == 0 or (parts.len == 1 and parts[0] == "list"): ckSafeImmediate
    else: ckMutating
  of ":provider":
    if parts.len == 0:
      ckSafeImmediate
    elif parts.len == 1 and parts[0] == "list":
      ckSafeImmediate
    elif parts.len >= 1 and parts[0] in ["add", "edit"]:
      ckModal
    else:
      ckMutating
  of ":model":
    if parts.len == 0 or (parts.len == 1 and parts[0] == "list"): ckSafeImmediate
    else: ckMutating
  of ":reasoning":
    if parts.len == 0 or (parts.len == 1 and parts[0] == "list"): ckSafeImmediate
    else: ckMutating
  of ":clear", ":summarize":
    ckMutating
  of ":quit", ":q", ":exit":
    ckQuit
  else:
    ckUnknown

proc completionFor*(line: string): seq[string] =
  let words = line.split(' ')
  if words.len == 0: return
  let last = words[^1]
  if words.len == 1:
    if last == "" or last.startsWith(":"):
      return @CommandNames
    return
  if words[0] == ":provider":
    if words.len == 2:
      for pr in activeProviders: result.add pr.name
      return
    if words.len == 3 and words[1] in ["edit", "rm", "remove"]:
      for pr in activeProviders: result.add pr.name
      return
  if words[0] == ":model" and words.len == 2:
    let prov = currentProvider()
    for m in orderedModels(prov):
      if experimentalEnabled or knownGoodFamily(prov.name, m) != "":
        result.add shortModel(m)
    return
  if words[0] == ":reasoning" and words.len == 2:
    if not experimentalEnabled:
      for r in ReasoningLevels: result.add r
    return
  if words[0] == ":streaming" and words.len == 2:
    result.add "on"
    result.add "off"
    return
  if words[0] == ":notify" and words.len == 2:
    result.add "on"
    result.add "off"
    return

proc readRequired*(editor: var minline.LineEditor, prompt: string,
                  hidden = false, noHistory = true): string =
  ## ctrl+c raises `minline.InputCancelled` to the caller; ctrl+d aborts
  ## the program. Empty input keeps re-prompting.
  while true:
    let s =
      if wizardReadLineHook != nil:
        wizardReadLineHook(prompt, hidden, noHistory).strip
      else:
        try: wizardReadLine(editor, prompt, hidechars = hidden,
                            noHistory = noHistory).strip
        except EOFError:
          stdout.write "\n"
          die "aborted", ExitConfig
    if s != "": return s

proc readOptional*(editor: var minline.LineEditor, prompt: string,
                  hidden = false, noHistory = true): string =
  ## ctrl+c raises `minline.InputCancelled` to the caller; ctrl+d aborts
  ## the program. Empty input is returned as "".
  if wizardReadLineHook != nil:
    return wizardReadLineHook(prompt, hidden, noHistory).strip
  try: wizardReadLine(editor, prompt, hidechars = hidden, noHistory = noHistory).strip
  except EOFError:
    stdout.write "\n"
    die "aborted", ExitConfig

# ---------- Provider wizard ----------

proc printSupported() =
  var seen: seq[string]
  for combo in KnownGoodCombos:
    if combo[0] notin seen: seen.add combo[0]
  subtleWriteLn(stdout, "  supported: " & seen.join(", "))

proc readProviderEntry(editor: var minline.LineEditor): string =
  let prevCb = editor.completionCallback
  editor.completionCallback = proc(ed: LineEditor): seq[string] =
    if experimentalEnabled:
      for (n, _) in ProviderCatalog: result.add n
    else:
      for combo in KnownGoodCombos:
        if combo[0] notin result: result.add combo[0]
  let label =
    if experimentalEnabled: "  provider name or url : "
    else: "  provider name        : "
  result = readRequired(editor, label)
  editor.completionCallback = prevCb

proc promptNameAndUrl(editor: var minline.LineEditor): (string, string) =
  let entry = readProviderEntry(editor)
  var name, url: string
  if experimentalEnabled and
     (entry.startsWith("http://") or entry.startsWith("https://")):
    url = entry.strip(chars = {'/', ' '})
    let suggested = defaultNameFromUrl(url)
    let namePrompt =
      if suggested == "": "  name                 : "
      else: &"  name [{suggested}]     : "
    name = readOptional(editor, namePrompt)
    if name == "": name = suggested
  else:
    name = entry
    let cu = catalogUrl(name)
    if experimentalEnabled:
      if cu != "":
        let urlEntry = readOptional(editor, &"  url [{cu}]     : ")
          .strip(chars = {'/', ' '})
        url = if urlEntry == "": cu else: urlEntry
      else:
        url = readRequired(editor, "  api base url         : ")
          .strip(chars = {'/', ' '})
    else:
      url = cu
  (name, url)

proc promptNewProvider*(editor: var minline.LineEditor): ProviderRec =
  printSupported()
  stdout.write "\n"
  # Empty input (just enter) skips key-based inference and drops straight
  # into `promptNameAndUrl`'s manual provider-name entry below — the path
  # local/keyless providers (e.g. ollama) need, since they have no key to
  # paste and infer from.
  var key = readOptional(editor, "  api key (enter to pick provider instead) : ",
                         hidden = true)
  # same key already configured?
  if key.len > 0:
    for pr in activeProviders:
      if pr.key == key:
        hintLn &"  already configured as {pr.name}", resetStyle
        return pr
  var name, url: string
  var inferred = inferProvider(key)
  if not experimentalEnabled and inferred != "" and
     curatedFor(inferred).len == 0:
    inferred = ""  # not in whitelist; fall through to manual entry
  if inferred != "":
    name = inferred
    url = catalogUrl(inferred)
    # same provider already exists? offer to update key instead
    for pr in activeProviders:
      if pr.name == name:
        hintLn "  detected: ", resetStyle, name, GreyFg,
               " -> already configured, updating key", Reset
        return ProviderRec(name: pr.name, url: pr.url, key: key,
                           models: pr.models)
    hintLn "  detected: ", resetStyle, name, GreyFg, " -> ", url, Reset
  else:
    while true:
      let (n, u) = promptNameAndUrl(editor)
      if n == "":
        errLn "name required"
        continue
      var clash = false
      for pr in activeProviders:
        if pr.name == n:
          clash = true
          break
      if clash:
        errLn &"name already used: {n}"
        continue
      name = n
      url = u
      break
  if key.len == 0:
    # Local servers (ollama, llama.cpp, ...) don't check the Authorization
    # header at all, but the config format and `loadProfile`/`buildProfile`
    # require a non-empty key for every provider. Fill in a harmless
    # placeholder rather than relaxing that invariant everywhere — a
    # provider that actually needs a real key will simply fail
    # `verifyProfile` below and prompt the user to re-enter one.
    key = name
  if not experimentalEnabled:
    let curated = curatedFor(name)
    for m in curated:
      hintLn "    ", resetStyle, shortModel(m)
    if curated.len == 0:
      # Provider not in known‑good list; give a clear hint.
      hintLn &"  provider {name} not known‑good; enable --experimental to use it", resetStyle
      raise newException(minline.InputCancelled, "")
    let lookup = shortToFull(curated)
    var prev = curated.mapIt(shortModel(it)).join(" ")
    let prevCb = editor.completionCallback
    editor.completionCallback = proc(ed: LineEditor): seq[string] =
      for m in curated: result.add shortModel(m)
    defer: editor.completionCallback = prevCb
    while true:
      let entered = readOptional(editor, &"  models [{prev}]  : ")
      let raw = if entered == "": prev else: entered
      let rawModels = splitModels(raw)
      var models: seq[string]
      var unknown: seq[string]
      for rm in rawModels:
        let resolved = lookup.getOrDefault(rm, rm)
        if resolved in curated:
          models.add resolved
        else:
          unknown.add rm
      if models.len == 0:
        errLn "need at least one model"
      elif unknown.len > 0:
        errLn "unknown known-good model: " & unknown.join(", ")
        prev = models.mapIt(shortModel(it)).join(" ")
      else:
        let prov = ProviderRec(name: name, url: url, key: key, models: models)
        let prof = Profile(name: name & "." & models[0], url: url,
                           key: key, model: models[0])
        hint "  verifying... ", resetStyle
        stdout.flushFile
        let (ok, err) = verifyProfile(prof)
        if ok:
          stdout.styledWriteLine fgGreen, styleBright, "ok", resetStyle
          return prov
        errLn "failed: " & err
        prev = models.mapIt(shortModel(it)).join(" ")
      let choice = readOptional(editor,
        "  [enter]=retry models, k=re-enter key, c=cancel : ").toLowerAscii
      if choice == "k":
        key = readRequired(editor,
          "  api key              : ", hidden = true)
      elif choice == "c":
        # User wants to abort the provider addition
        raise newException(minline.InputCancelled, "cancelled by user")
  hint "  fetching models...   ", resetStyle
  stdout.flushFile
  let (available, fetchErr) = fetchModels(url, key)
  let sortedAvailable = available.sorted
  let lookup = shortToFull(sortedAvailable)
  if fetchErr.len > 0:
    errLn "unavailable — ", fetchErr
  elif sortedAvailable.len == 0:
    hintLn "unavailable — enter manually", resetStyle
  else:
    hintLn &"{sortedAvailable.len} available", resetStyle
    for m in sortedAvailable:
      hintLn "    ", resetStyle, shortModel(m)
  let prevCb = editor.completionCallback
  editor.completionCallback = proc(ed: LineEditor): seq[string] =
    for m in sortedAvailable: result.add shortModel(m)
  defer: editor.completionCallback = prevCb
  # Pre-populate with known-good models for this provider (KnownGoodCombos order).
  var knownGoodInit: seq[string]
  for combo in KnownGoodCombos:
    if combo[0].toLowerAscii == name.toLowerAscii:
      for avail in sortedAvailable:
        if avail == combo[1]:
          knownGoodInit.add shortModel(combo[1])
          break
  var prev = knownGoodInit.join(" ")
  while true:
    let prompt =
      if prev == "": "  models (space-sep.)  : "
      else: &"  models [{prev}]  : "
    let entered = readOptional(editor, prompt)
    let raw = if entered == "": prev else: entered
    let rawModels = splitModels(raw)
    # Resolve each entered name (short or full) to its full id using the
    # fetched list. If the user typed a short name, `lookup` resolves it;
    # if they typed a full id that was in the list, it passes through
    # unchanged; unknown names are kept as-is.
    var models: seq[string]
    for rm in rawModels:
      models.add lookup.getOrDefault(rm, rm)
    if models.len == 0:
      errLn "need at least one model"
      continue
    let prov = ProviderRec(name: name, url: url, key: key, models: models)
    let prof = Profile(name: name & "." & models[0], url: url,
                       key: key, model: models[0])
    hint "  verifying... ", resetStyle
    stdout.flushFile
    let (ok, err) = verifyProfile(prof)
    if ok:
      stdout.styledWriteLine fgGreen, styleBright, "ok", resetStyle
      return prov
    errLn "failed: " & err
    prev = models.mapIt(shortModel(it)).join(" ")
    let choice = readOptional(editor,
      "  [enter]=retry models, k=re-enter key, c=cancel : ").toLowerAscii
    if choice == "k":
      key = readRequired(editor,
        "  api key              : ", hidden = true)
    elif choice == "c":
      raise newException(minline.InputCancelled, "cancelled by user")

proc promptEditProvider*(editor: var minline.LineEditor,
                        existing: ProviderRec): ProviderRec =
  hintLn &"  editing '{existing.name}' (enter to keep; ctrl+c/esc clears line, empty line aborts)",
    resetStyle
  while true:
    let newName = readOptional(editor,
      &"  name [{existing.name}]  : ")
    let name = if newName == "": existing.name else: newName
    if name != existing.name:
      var clash = false
      for pr in activeProviders:
        if pr.name != existing.name and pr.name == name:
          clash = true
          break
      if clash:
        errLn &"name already used: {name}"
        continue
    let newUrl = readOptional(editor,
      &"  url [{existing.url}]  : ").strip(chars = {'/', ' '})
    let url = if newUrl == "": existing.url else: newUrl
    let newKey = readOptional(editor,
      "  api key [keep existing] : ", hidden = true)
    let key = if newKey == "": existing.key else: newKey
    hint "  fetching models...   ", resetStyle
    stdout.flushFile
    let (available, fetchErr) = fetchModels(url, key)
    let sortedAvailable = available.sorted
    let prevCb = editor.completionCallback
    editor.completionCallback = proc(ed: LineEditor): seq[string] =
      sortedAvailable.mapIt(shortModel(it))
    defer: editor.completionCallback = prevCb
    if fetchErr.len > 0:
      errLn "unavailable — ", fetchErr
    elif sortedAvailable.len == 0:
      hintLn "  unavailable — enter manually", resetStyle
    else:
      hintLn &"  {sortedAvailable.len} available", resetStyle
      for m in sortedAvailable:
        hintLn "    ", resetStyle, shortModel(m)
    let modelsCurrent = existing.models.mapIt(shortModel(it)).join(" ")
    let newModels = readOptional(editor,
      &"  models [{modelsCurrent}]  : ")
    let rawModels = if newModels == "": existing.models
                   else: splitModels(newModels)
    # Resolve short names against the fetched model list; unknown names
    # pass through as-is (full id entered by the user).
    let lookup = shortToFull(sortedAvailable)
    let models = rawModels.mapIt(lookup.getOrDefault(it, it))
    if models.len == 0:
      errLn "need at least one model"
      continue
    let prof = Profile(name: name & "." & models[0], url: url,
                       key: key, model: models[0])
    hint "  verifying... ", resetStyle
    stdout.flushFile
    let (ok, err) = verifyProfile(prof)
    if ok:
      stdout.styledWriteLine fgGreen, styleBright, "ok", resetStyle
      return ProviderRec(name: name, url: url, key: key, models: models)
    errLn "failed: " & err

proc bootstrapProvider*(editor: var minline.LineEditor): Profile =
  stdout.styledWriteLine fgMagenta,
    "no provider configured, let's add one. (ctrl+c to abort; esc clears line)",
    resetStyle
  let prov = try: promptNewProvider(editor)
             except minline.InputCancelled:
               die "aborted", ExitConfig
  activeProviders.add prov
  activeCurrent = prov.name & "." & firstModel(prov)
  writeConfigFile(configPath(), activeCurrent, activeProviders)
  hintLn &"  saved to {configPath()}", resetStyle
  # The wizard's last `wizardReadLine` left `inputModalActive` held so
  # the input thread could not race these post-writes; release it now that
  # the config write and the "saved to" line have flushed, matching the
  # `wizardFinish` the main loop calls after a `:provider` cdModal command.
  wizardFinish()
  buildProfile(activeCurrent, activeProviders, "")

# ---------- Provider / model commands ----------

proc cmdProviderList(prof: Profile) =
  if activeProviders.len == 0:
    hintLn "  no providers", resetStyle
    return
  let curName = if prof.name == "": "" else: prof.name.split('.')[0]
  for pr in activeProviders:
    let current = pr.name == curName
    let mark = if current: "*" else: " "
    let tail = if current: &"  [{shortModel(prof.model)}]" else: ""
    if not experimentalEnabled and not hasKnownGoodModel(pr):
      subtleWriteLn(stdout,
        "  " & mark & " " & pr.name & tail)
    else:
      hintLn "  ", mark, " ", resetStyle, pr.name, tail

proc cmdProviderSelect(target: string, prof: var Profile) =
  var prov: ProviderRec
  var found = false
  for pr in activeProviders:
    if pr.name == target:
      prov = pr
      found = true
      break
  if not found:
    errLn &"unknown provider: {target}"
    return
  if prov.models.len == 0:
    errLn &"provider {target} has no models"
    return
  let newCurrent = prov.name & "." & firstModel(prov)
  let candidate = buildProfile(newCurrent, activeProviders, "")
  activeCurrent = newCurrent
  prof = candidate
  writeConfigFile(configPath(), activeCurrent, activeProviders)
  showProfile(prof)
  if not gateExperimental(candidate):
    explainExperimentalGate(candidate)

proc cmdProviderAdd(editor: var minline.LineEditor, prof: var Profile) =
  # Cancel propagates from `wizardReadLine` through `promptNewProvider`
  # back to `handleCommandResult`, which turns it into an empty
  # `cdModal` return. No message, no state change — the prompt is
  # repainted by the input thread's cancel handler before we get
  # here.
  let prov = promptNewProvider(editor)
  for pr in activeProviders:
    if pr.name == prov.name:
      errLn &"duplicate provider: {prov.name} already configured"
      return
  activeProviders.add prov
  if activeCurrent == "":
    activeCurrent = prov.name & "." & firstModel(prov)
  writeConfigFile(configPath(), activeCurrent, activeProviders)
  if prof.name == "":
    prof = buildProfile(activeCurrent, activeProviders, "")
  hintLn &"  added {prov.name}", resetStyle
  showProfile(prof)

proc cmdProviderEdit(target: string, editor: var minline.LineEditor,
                     prof: var Profile) =
  var idx = -1
  for i, pr in activeProviders:
    if pr.name == target: idx = i; break
  if idx < 0:
    errLn &"unknown provider: {target}"
    return
  let updated = promptEditProvider(editor, activeProviders[idx])
  activeProviders[idx] = updated
  let curName = if activeCurrent == "": "" else: activeCurrent.split('.')[0]
  if curName == target:
    let wantedModel = prof.model
    let model =
      if updated.findModel(wantedModel) >= 0: wantedModel
      else: firstModel(updated)
    activeCurrent = updated.name & "." & model
    prof = buildProfile(activeCurrent, activeProviders, "")
  writeConfigFile(configPath(), activeCurrent, activeProviders)
  hintLn &"  updated {target}", resetStyle

proc cmdProviderRm(target: string, prof: var Profile) =
  var idx = -1
  for i, pr in activeProviders:
    if pr.name == target: idx = i; break
  if idx < 0:
    errLn &"unknown provider: {target}"
    return
  activeProviders.delete(idx)
  let curName = if activeCurrent == "": "" else: activeCurrent.split('.')[0]
  if curName == target:
    if activeProviders.len > 0:
      let np = activeProviders[0]
      activeCurrent = np.name & "." & firstModel(np)
      prof = buildProfile(activeCurrent, activeProviders, "")
    else:
      activeCurrent = ""
      prof = Profile()
  writeConfigFile(configPath(), activeCurrent, activeProviders)
  hintLn &"  removed {target}", resetStyle

proc cmdProvider(arg: string, editor: var minline.LineEditor,
                 prof: var Profile) =
  let parts = arg.splitWhitespace()
  if parts.len == 0 or (parts.len == 1 and parts[0] == "list"):
    cmdProviderList(prof)
    return
  case parts[0]
  of "add":
    if parts.len != 1:
      errLn "usage: :provider add"
    else:
      cmdProviderAdd(editor, prof)
  of "edit":
    if parts.len != 2:
      errLn "usage: :provider edit <name>"
    else:
      cmdProviderEdit(parts[1], editor, prof)
  of "rm", "remove":
    if parts.len != 2:
      errLn &"usage: :provider {parts[0]} <name>"
    else:
      cmdProviderRm(parts[1], prof)
  else:
    if parts.len != 1:
      errLn "usage: :provider [<name> | add | rm <name>]"
    else:
      cmdProviderSelect(parts[0], prof)

proc cmdModelList(prof: Profile) =
  let prov = currentProvider()
  if prov.name == "":
    hintLn "  no provider selected", resetStyle
    return
  if prov.models.len == 0:
    hintLn &"  {prov.name}: no models", resetStyle
    return
  for m in orderedModels(prov):
    let mark = if m == prof.model: "*" else: " "
    let short = shortModel(m)
    let kg = knownGoodFamily(prov.name, m)
    if kg == "" and not experimentalEnabled:
      subtleWriteLn(stdout, "  " & mark & " " & short)
    else:
      let kgSuffix = if experimentalEnabled and kg != "": "*" else: ""
      hintLn "  ", mark, " ", resetStyle, short & kgSuffix, resetStyle

proc cmdModelSelect(target: string, prof: var Profile) =
  let prov = currentProvider()
  if prov.name == "":
    errLn "no provider selected"
    return
  let idx = prov.findModel(target)
  if idx < 0:
    errLn &"unknown model: {target}"
    return
  let fullModel = prov.models[idx]
  let newCurrent = prov.name & "." & fullModel
  let candidate = buildProfile(newCurrent, activeProviders, "")
  if not gateExperimental(candidate):
    explainExperimentalGate(candidate)
    return
  activeCurrent = newCurrent
  prof = candidate
  writeConfigFile(configPath(), activeCurrent, activeProviders)
  showProfile(prof)

proc cmdModel(arg: string, prof: var Profile) =
  let parts = arg.splitWhitespace()
  case parts.len
  of 0:
    cmdModelList(prof)
  of 1:
    if parts[0] == "list":
      cmdModelList(prof)
    else:
      cmdModelSelect(parts[0], prof)
  else:
    errLn "usage: :model [<name>]"

proc cmdReasoningList(prof: Profile) =
  let prov = providerForProfile(prof)
  if prov.name == "":
    hintLn "  no provider selected", resetStyle
    return
  if experimentalEnabled:
    let cur = if prof.reasoning == "": "(none)" else: prof.reasoning
    hintLn "  reasoning: ", resetStyle, cur
    hintLn "  experimental: level is free-form, type any value", resetStyle
    return
  let levels = availableReasonings(prov, prof.family, prof.model)
  if levels.len == 0:
    hintLn &"  {prof.family}: no reasoning knob", resetStyle
    return
  for r in levels:
    let mark = if r == prof.reasoning: "*" else: " "
    hintLn "  ", mark, " ", resetStyle, r

proc cmdReasoningSelect(target: string, prof: var Profile) =
  let prov = providerForProfile(prof)
  if prov.name == "":
    errLn "no provider selected"
    return
  let value = target.toLowerAscii
  if not experimentalEnabled:
    let levels = availableReasonings(prov, prof.family, prof.model)
    if value notin levels:
      errLn &"unknown reasoning level: {target} (choose from {levels.join(\" \")})"
      return
  prof.reasoning = value
  for i, pr in activeProviders:
    if pr.name == prov.name:
      activeProviders[i].reasoning = value
      break
  writeConfigFile(configPath(), activeCurrent, activeProviders)
  showProfile(prof)

proc cmdReasoning(arg: string, prof: var Profile) =
  let parts = arg.splitWhitespace()
  case parts.len
  of 0:
    cmdReasoningList(prof)
  of 1:
    if parts[0] == "list":
      cmdReasoningList(prof)
    else:
      cmdReasoningSelect(parts[0], prof)
  else:
    errLn "usage: :reasoning [<level>]"

proc cmdStreamingList() =
  let mark = if streamingEnabled: "on" else: "off"
  hintLn "  streaming: ", mark,
    "  (on = live SSE output, off = single request/response)", resetStyle

proc cmdStreamingSelect(target: string) =
  case target.toLowerAscii
  of "on":
    streamingEnabled = true
  of "off":
    streamingEnabled = false
  else:
    errLn &"unknown value: {target} (choose on or off)"
    return
  writeConfigFile(configPath(), activeCurrent, activeProviders)
  cmdStreamingList()

proc cmdStreaming(arg: string) =
  let parts = arg.splitWhitespace()
  case parts.len
  of 0:
    cmdStreamingList()
  of 1:
    if parts[0] == "list":
      cmdStreamingList()
    else:
      cmdStreamingSelect(parts[0])
  else:
    errLn "usage: :streaming [on|off]"

proc cmdNotifyList() =
  let mark = if notifyEnabled: "on" else: "off"
  hintLn "  notify: ", mark,
    "  (on = desktop notification when a turn ends, off = silent)", resetStyle

proc cmdNotifySelect(target: string) =
  case target.toLowerAscii
  of "on":
    notifyEnabled = true
  of "off":
    notifyEnabled = false
  else:
    errLn &"unknown value: {target} (choose on or off)"
    return
  writeConfigFile(configPath(), activeCurrent, activeProviders)
  cmdNotifyList()

proc cmdNotify(arg: string) =
  let parts = arg.splitWhitespace()
  case parts.len
  of 0:
    cmdNotifyList()
  of 1:
    if parts[0] == "list":
      cmdNotifyList()
    else:
      cmdNotifySelect(parts[0])
  else:
    errLn "usage: :notify [on|off]"

proc nearestCommand(name: string): string =
  var bestDist = high(int)
  for c in CommandNames:
    let d = levenshtein(name.toLowerAscii, c.toLowerAscii)
    if d < bestDist:
      bestDist = d
      result = c
  if bestDist > 2: result = ""

proc commandTitle(name, arg: string; ok: bool): string =
  if not ok and name notin CommandNames:
    return "command"
  case name
  of ":?":
    "help"
  of ":provider":
    let parts = arg.splitWhitespace()
    if parts.len == 0:
      "providers"
    elif parts[0] in ["add", "edit", "rm", "remove"]:
      "provider " & (if parts[0] == "remove": "rm" else: parts[0])
    else:
      "profile"
  of ":model":
    if arg.len == 0: "models" else: "profile"
  of ":reasoning":
    if arg.len == 0: "reasoning" else: "profile"
  of ":streaming":
    "streaming"
  of ":notify":
    "notify"
  else:
    name.strip(chars = {':'})

# ---------- Session preamble + user-input prep ----------

proc loadAgentsMd(start: string): string =
  ## Walk from `start` up to the filesystem root, collecting project-notes
  ## files in precedence order. At each directory level, `3CODE.md` is read
  ## before `AGENTS.md` (both load when both exist). The deepest level (cwd)
  ## is emitted first, so more-deeply-nested files take precedence by virtue
  ## of appearing earlier in the developer message.
  var dir = resolvePath(start)
  while true:
    for name in ["3CODE.md", "AGENTS.md"]:
      let candidate = dir / name
      if not fileExists(candidate): continue
      try:
        let body = readFile(candidate)
        if isBinaryContent(body): continue
        if result.len > 0: result.add "\n\n"
        result.add "# " & candidate & "\n\n" & body
      except CatchableError: discard
    let parent = parentDir(dir)
    if parent == dir or parent == "": break
    dir = parent

proc shellCapture(cmd: string, timeoutS = 3): string =
  ## Run a short shell command via `sh -c` and return its stdout (trimmed).
  ## Empty on failure — used purely to gather context, so failures are silent.
  ## `cmd` must be a literal, never user-controlled input; no shell escaping
  ## is performed.
  let tmp = getTempDir() / ("3code_ctx_" & $getCurrentProcessId() & "_" & $epochTime().int64)
  createDir(tmp)
  let outPath = tmp / "out"
  let wrapped = when defined(windows):
    let b = resolveBash()
    if b.len == 0: return ""
    # `timeout` (MSYS2 coreutils) bounds the run; the redirect lives outside
    # bash quotes so cmd.exe handles it with Windows-correct `2>nul`.
    &"{b} -lc \"timeout {timeoutS}s {cmd}\" >\"{outPath}\" 2>nul"
  else:
    &"timeout {timeoutS}s sh -c \"{cmd}\" >\"{outPath}\" 2>/dev/null"
  discard execShellCmd(wrapped)
  result =
    if fileExists(outPath): readFile(outPath).strip
    else: ""
  try: removeDir(tmp) except CatchableError: discard

proc sessionPreamble*(cwd: string): string =
  ## Build a one-shot context block to prepend to the first user message of
  ## a fresh session: cwd, git state, top-level listing, AGENTS.md content.
  var lines: seq[string]
  let displayCwd = collapseHome(cwd)
  lines.add "cwd: " & displayCwd
  let inGit = shellCapture("git rev-parse --is-inside-work-tree") == "true"
  if inGit:
    let branch = shellCapture("git rev-parse --abbrev-ref HEAD")
    let dirty = shellCapture("git status --porcelain | wc -l")
    var gitLine = "git: " & (if branch == "": "(detached)" else: branch)
    if dirty != "" and dirty != "0":
      gitLine.add ", " & dirty & " uncommitted"
    lines.add gitLine
    let recent = shellCapture("git log --oneline -3")
    if recent != "":
      lines.add "recent commits:"
      for l in recent.splitLines:
        let s = l.strip
        if s.len == 0: continue
        let trimmed = if s.len > 80: utf8ByteCut(s, 77) & "..." else: s
        lines.add "  " & trimmed
  let listing = shellCapture("ls -1 --color=never | head -30")
  if listing != "":
    let entries = listing.splitLines.filterIt(it.strip.len > 0)
    lines.add "files in cwd: " & entries.join(" ")
  let notes = loadAgentsMd(cwd)
  result = "<session_context>\n" & lines.join("\n") & "\n</session_context>"
  if notes.len > 0:
    result.add "\n\n<project_notes>\n" & notes & "\n</project_notes>"

proc inlineAtFiles*(msg: string): string =
  ## Find @path tokens (whitespace-delimited, must follow whitespace or start
  ## of input). For each that resolves to an existing regular file under cwd,
  ## append `\n\n=== {path} ===\n<content>` (capped) to the message. Leave the
  ## @token visible so the model sees the user's intent.
  result = msg
  var seen: seq[string]
  var i = 0
  while i < msg.len:
    let prevOk = i == 0 or msg[i-1] in {' ', '\t', '\n'}
    if prevOk and msg[i] == '@' and i + 1 < msg.len and msg[i+1] notin {' ', '\t', '\n', '@'}:
      var j = i + 1
      while j < msg.len and msg[j] notin {' ', '\t', '\n'}:
        inc j
      let raw = msg[i+1 ..< j]
      let path = resolvePath(raw)
      if path notin seen and fileExists(path):
        seen.add path
        const Cap = 64 * 1024
        let content =
          try:
            let s = readFile(path)
            if isBinaryContent(s): "[binary file: " & raw & " — skipped]"
            elif s.len > Cap: utf8ByteCut(s, Cap) & "\n... [truncated; file is " & $s.len & " bytes]"
            else: s
          except CatchableError as e:
            "[error reading file: " & e.msg & "]"
        result.add "\n\n=== " & raw & " ===\n" & content
      i = j
    else:
      inc i

proc isFirstUserMessage*(messages: JsonNode): bool =
  if messages == nil or messages.kind != JArray: return true
  for m in messages:
    if m.kind == JObject and m{"role"}.getStr == "user":
      return false
  true

proc buildUserMessage*(messages: JsonNode, raw: string): string =
  ## Apply @file inlining always; prepend the session preamble (cwd, git
  ## state, AGENTS.md, ls) only on the first user message of a session so
  ## resumed conversations don't re-inject stale context.
  let body = inlineAtFiles(raw)
  if isFirstUserMessage(messages):
    sessionPreamble(safeCwd()) & "\n\n" & body
  else:
    body

proc readInput*(editor: var minline.LineEditor, done: var bool): string =
  ## Read a line submitted by the persistent input thread. The same
  ## ``minline.readLineWith`` path owns idle prompt input and active-turn
  ## buffered input; the controller only consumes completed lines here.
  ensureInputThreadStarted()
  while true:
    if not inputThreadRunning and inputEditor != nil:
      ensureInputThreadStarted()
    var line = ""
    var echoRows = 0
    var cmdWasQuit = false
    var wasInterrupt = false
    if consumeQueuedInput(line, echoRows, cmdWasQuit, wasInterrupt):
      navigatedUp = false
      editor.echoRows = echoRows
      if line.strip == "":
        resetPromptInputAfterEmpty(editor.echoRows)
        releaseIdleSubmittedInput()
        return ""
      return line
    if wasInterrupt:
      # An idle Ctrl-C / ESC: the input thread already repainted the empty
      # prompt in place, so no walk-back. Just clear the idle-submitted flag
      # and return to the prompt loop.
      navigatedUp = false
      releaseIdleSubmittedInput()
      return ""
    if cmdWasQuit:
      done = true
      return ""
    sleep 5

# ---------- Command dispatcher ----------

proc handleCommand*(cmd: string, messages: var JsonNode, session: var Session,
                   prof: var Profile, editor: var minline.LineEditor): bool =
  ## returns true if the input was a recognised command
  let res = handleCommandResult(cmd, messages, session, prof, editor)
  if not res.recognized:
    return false
  if res.body.len > 0:
    stdout.write res.body
    stdout.flushFile
  true

proc handleCommandResult*(cmd: string, messages: var JsonNode,
                          session: var Session, prof: var Profile,
                          editor: var minline.LineEditor): CommandResult =
  ## Execute a REPL command and return its terminal body instead of writing
  ## directly to scrollback. Command internals still use the legacy display
  ## helpers; their stdout is captured here so the outer controller can commit
  ## one high-level transcript item.
  let c = cmd.strip
  if c.len == 0 or c[0] != ':':
    return CommandResult(recognized: false)
  let sp = c.find({' ', '\t'})
  let name = if sp < 0: c else: c[0 ..< sp]
  let arg = if sp < 0: "" else: c[sp+1 .. ^1].strip
  let kind = classifyCommand(c)
  if kind == ckModal:
    stdout.write "\r\n"
    stdout.flushFile
    # The modal wizard runs each prompt on the input thread (see
    # `wizardReadLine`), so it owns `inputModalActive` and the
    # per-field save/restore dance itself. The controller only needs
    # to call the wizard and let it return. Cancel propagates as
    # `minline.InputCancelled` from `wizardReadLine`; we let it
    # propagate up to the outer `try` in `handleCommandResult` and
    # turn it into an empty `cdModal` return so the main loop
    # restores its idle state.
    try:
      case name
      of ":provider":
        cmdProvider(arg, editor, prof)
        session.profileName = prof.name
      else:
        discard
    except minline.InputCancelled:
      discard
    return CommandResult(recognized: true, ok: true,
                         name: commandTitle(name, arg, true),
                         disposition: cdModal)
  var ok = true
  let body = captureStdoutWrites:
    case name
    of ":help", ":?":
      renderHelp()
    of ":tokens":
      if session.usage.totalTokens == 0:
        cmdResponse "no tokens used yet"
      else:
        let fresh = max(0, session.usage.promptTokens - session.usage.cachedTokens)
        let line = tokenSlot("↑", fresh) &
          "  " & tokenSlot("↻", session.usage.cachedTokens) &
          "  " & tokenSlot("↓", session.usage.completionTokens) &
          "  total " & humanTokens(session.usage.totalTokens)
        cmdResponse line
    of ":clear":
      messages = %* [{"role": "system", "content": buildSystemPrompt(prof)}]
      session.toolLog.setLen 0
      session.usage = Usage()
      session.lastPromptTokens = 0
      session.readCache = nil
      session.plan.setLen 0
      emitFatPromptEvent clearPendingHintEvent()
      emitFatPromptEvent clearBarEvent()
      if session.savePath != "":
        clearDraft(session)
        releaseSessionLock(session.savePath)
        session.savePath = newSessionPath()
        session.created = $now()
        session.cwd = safeCwd()
        acquireSessionLock(session.savePath)
      cmdResponse "════════════════════════════════════════"
    of ":model":
      cmdModel(arg, prof)
      session.profileName = prof.name
    of ":provider":
      cmdProvider(arg, editor, prof)
      session.profileName = prof.name
    of ":reasoning":
      cmdReasoning(arg, prof)
    of ":streaming":
      cmdStreaming(arg)
    of ":notify":
      cmdNotify(arg)
    of ":prompt":
      cmdResponse buildSystemPrompt(prof)
    of ":version":
      cmdResponse "3code v" & Version
    of ":show":
      showTool(arg, session.toolLog)
    of ":log":
      listTools(session.toolLog)
    of ":sessions":
      # Listing is directory-scoped by design; the full set lives under
      # `sessionDir()`. `showCwd` is threaded through as false to keep
      # the re-enable path a one-line flip here and in the `-l` handler.
      let showCwd = false
      let askedAll = arg.strip.toLowerAscii in ["all", "-a", "--all"]
      let paths = listSessionPathsForCwd(safeCwd())
      if paths.len == 0:
        cmdResponse "no saved sessions for this directory"
      else:
        printSessionList(paths, session.savePath, showCwd)
      if askedAll:
        let dir = collapseHome(sessionDir())
        cmdResponse "listing is scoped to this directory — run from " & dir &
                    " for all"
    of ":summarize":
      if prof.name == "":
        ok = false
        cmdError "no provider configured. use :provider add"
      else:
        let n = summarizeHistory(messages, prof)
        if n == 0:
          ok = false
          cmdResponse "failed or not worth it"
        else:
          cmdResponse &"collapsed {n} message" &
            (if n == 1: "" else: "s") &
            " into a synthetic recap"
          saveSession(session, messages)
    else:
      ok = false
      let suggestion = nearestCommand(name)
      if suggestion != "":
        cmdError "unknown command: " & c & "  did you mean " & suggestion & "?"
      else:
        cmdError "unknown command: " & c & "  (try :help)"
  let title = commandTitle(name, arg, ok)
  let disposition =
    case kind
    of ckSafeImmediate: cdHarnessOnly
    of ckModal: cdModal
    else: cdTranscriptResult
  CommandResult(recognized: true, ok: ok, name: title,
                body: body,
                plainBody: ok and (title == "profile" or title == "clear"),
                clearFooter: ok and title == "profile",
                disposition: disposition)
